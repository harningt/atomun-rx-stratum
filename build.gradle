import org.ajoberstar.gradle.git.release.opinion.Strategies

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'org.ajoberstar.release-opinion' version '1.5.1'
    id 'org.ajoberstar.defaults' version '0.6.0'
    id "org.standardout.versioneye" version "1.4.0"
    id 'checkstyle'
    id 'findbugs'
}

group = 'us.eharning.atomun'
description = 'Utility network library to access Stratum servers.'
sourceCompatibility = 1.8

defaults {
    id = 'harningt'

    developers = [
            [id: 'harningt', name: 'Thomas Harning Jr', email: 'harningt@gmail.com']
    ]

    // used by license plugin
    copyrightYears = '2016'
}

license {
    skipExistingHeaders true
}

release {
    // Adds SNAPSHOT strategy to allowed list to be used by Travis-CI
    versionStrategy Strategies.SNAPSHOT
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    findbugs 'com.google.code.findbugs:findbugs:3.0.1'

    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    compile group: 'io.reactivex', name: 'rxjava', version: '1.1.8'

    compile 'com.google.code.findbugs:annotations:3.0.1'
    compile 'com.google.code.findbugs:jsr305:3.0.1'

    testCompile 'org.codehaus.groovy:groovy:2.4.7'
    testCompile 'cglib:cglib-nodep:3.2.4'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'net.trajano.commons:commons-testing:2.1.0'
    testCompile 'net.trajano.caliper:caliper:1.2.1'
    testCompile 'org.yaml:snakeyaml:1.17'
}

checkstyle {
    /* Using checkstyle 6.19 as that is the last version of Checkstyle to support JDK7 */
    toolVersion = '6.19'
    configFile = new File(rootDir, 'gradle/checkstyle/checkstyle.xml')
}

jacoco {
    toolVersion = '0.7.7.201606060606'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.destination "${buildDir}/jacocoHtml"
    }
}

sonarRunner {
    toolVersion = '2.4'
}

findbugs {
    sourceSets = [ sourceSets.main ]
}

javadoc {
    options.tags "todo:a:TODO:"
}

apply from: "$rootDir/gradle/maven-publish.gradle"

wrapper {
    gradleVersion = '4.0'
}
